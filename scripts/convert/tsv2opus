#!/usr/bin/env perl
#-*-perl-*-
#

=head1 NAME

tsv2opus

=head1 USAGE

=head1 OPTIONS

 -S <field> ........ position of the source language string (default = 0)
 -T <field> ........ position of the target language string (default = 1)
 -P <field> ........ position of the alignment probability/score (default = 2)
 -O <field> ........ position of the origin information (default = 3)

=head1 DESCRIPTION

C<tsv2opus> converts a sentence-aligned corpus in plain text format (TAB separated sentences) into the OPUS XML-based format. It also does sentence-splitting based on Lingua::Sentence.


=head1 LICENSE

 ---------------------------------------------------------------------------
 Copyright (c) 2004-2019 Joerg Tiedemann

 Permission is hereby granted, free of charge, to any person obtaining a copy
 of this software and associated documentation files (the "Software"), to deal
 in the Software without restriction, including without limitation the rights
 to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 copies of the Software, and to permit persons to whom the Software is
 furnished to do so, subject to the following conditions:

 The above copyright notice and this permission notice shall be included in all
 copies or substantial portions of the Software.

 THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 SOFTWARE.
 ---------------------------------------------------------------------------

=head1 See also

This script is part of opus-tools L<https://github.com/Helsinki-NLP/opus-tools>
See also OPUS L<http://opus.nlpl.eu> for more information about the corpus.

=cut


use strict;
use FindBin qw($Bin);
use lib $Bin.'/../../lib';

use OPUS::Tools;
use IO::File;
use File::Basename qw/dirname basename/;
use Locale::Language;
use XML::Writer;
use Lingua::Sentence;

use vars qw($opt_r $opt_s $opt_t $opt_b $opt_z $opt_p $opt_l $opt_d $opt_D $opt_M
    $opt_S $opt_T $opt_P $opt_O);
use Getopt::Std;
getopts('dDrs:t:b:zp:l:M:S:T:P:O:');

my $srclang  = $opt_s || 'xx';
my $trglang  = $opt_t || 'yy';
my $filebase = $opt_b || 'corpus';
my $maxsize  = $opt_p;          # split into parts of size <p>
my $part     = $maxsize || $opt_D ? 1 : 0;

my %fields   = ();
$fields{src} = defined($opt_S) ? $opt_S : 0;
$fields{trg} = defined($opt_T) ? $opt_T : 1;
$fields{score} = defined($opt_P) ? $opt_P : 2;
$fields{origin} = defined($opt_O) ? $opt_O : 3;

binmode(STDIN,":encoding(utf-8)");

if ($opt_r){
    $srclang=~s/\_.*$// unless ($srclang=~/^zh/);
    $trglang=~s/\_.*$// unless ($trglang=~/^zh/);
}

my $algdir = $srclang gt $trglang ? "$trglang-$srclang" : "$srclang-$trglang";
my $algfile = $opt_z ? $algdir.'/'.$filebase.'.xml.gz' : $algdir.'/'.$filebase.'.xml';
system('mkdir -p '.$algdir);
my $AlgOut = $opt_z ? IO::File->new("| gzip -c > $algfile") : IO::File->new(">$algfile");


## print align file header
print $AlgOut '<?xml version="1.0" encoding="utf-8"?>
<!DOCTYPE cesAlign PUBLIC "-//CES//DTD XML cesAlign//EN" "">
<cesAlign version="1.0">
';


my ($SrcWriter,$TrgWriter) = open_output($filebase,$srclang,$trglang,$part);

## only allow 2-letter codes, fallbak to English
my ($srcSplitLang,$trgSplitLang) = ($srclang,$trglang);

$srcSplitLang =~s/\_.*$//;
$trgSplitLang =~s/\_.*$//;
$srcSplitLang = 'en' unless ($srclang=~/^..$/);
$trgSplitLang = 'en' unless ($trglang=~/^..$/);

## create sentence splitters
my $SrcSplitter = Lingua::Sentence->new($srcSplitLang);
my $TrgSplitter = Lingua::Sentence->new($trgSplitLang);



## read the corpus and convert
my $sid = 0;
my $lid = 0;
while (<>){
    chomp;
    my @f      = split(/\t/);
    my $src    = $f[$fields{src}];
    my $trg    = $f[$fields{trg}];
    my $score  = $f[$fields{score}]  if ($#f >= $fields{score});
    my $origin = $f[$fields{origin}] if ($#f >= $fields{origin});
#     my ($src,$trg,$score) = split(/\t/);


    ## document boundary? add a new paragraph or a new document
    if ($src eq '' && $trg eq ''){
	if ($opt_D){
	    ## if smaller than minimum size of a document
	    ## ---> concatenate and don't create a new one
	    if ($opt_M && $sid < $opt_M){
		$SrcWriter->emptyTag('p');
		$TrgWriter->emptyTag('p');
	    }
	    else{
		$sid = 0;
		$part++;
		close_output($SrcWriter,$TrgWriter);
		($SrcWriter,$TrgWriter) = 
		    open_output($filebase,$srclang,$trglang,$part);
	    }
	}
	else{
	    $SrcWriter->emptyTag('p');
	    $TrgWriter->emptyTag('p');
	}
	next;
    }

    $sid++;
    if ($maxsize && $sid>$maxsize){
	$sid = 0;
	$part++;
	close_output($SrcWriter,$TrgWriter);
	($SrcWriter,$TrgWriter) = 
	    open_output($filebase,$srclang,$trglang,$part);
    }

    my @SrcSents = $SrcSplitter->split_array($src);
    my @TrgSents = $TrgSplitter->split_array($trg);

    my @SrcIds = write_sentences($SrcWriter,$sid,\@SrcSents,$origin);
    my @TrgIds = write_sentences($TrgWriter,$sid,\@TrgSents,$origin);

    $lid++;
    print $AlgOut '<link xtargets="';
    print $AlgOut join(' ',@SrcIds);
    print $AlgOut ';';
    print $AlgOut join(' ',@TrgIds);
    print $AlgOut '" id="SL'.$lid;

    ## add certainty if likelihood file is given
    if ($score){
	print $AlgOut '" score="'.$score;
    }
    print $AlgOut '"/>',"\n";
    print STDERR '.' if (! ($lid % 1000));
    print STDERR " $lid\n" if (! ($lid % 50000));
}

close_output($SrcWriter,$TrgWriter);
print $AlgOut "</cesAlign>\n";



sub open_output{
    my ($filebase,$srclang,$trglang,$part) = @_;
    my ($srcxml,$trgxml);

    ## split into parts
    if ($part){
	my $ext = sprintf('%04d',$part);
	$srcxml = $srclang.'/'.$filebase.'.'.$ext.'.xml';
	$trgxml = $trglang.'/'.$filebase.'.'.$ext.'.xml';
    }
    else{
	$srcxml = $srclang.'/'.$filebase.'.xml';
	$trgxml = $trglang.'/'.$filebase.'.xml';
    }

    ## create corpus directories
    system("mkdir -p ".dirname($srcxml));
    system("mkdir -p ".dirname($trgxml));

    ## open corpus files
    my ($SrcOut,$TrgOut);
    if ($opt_z){
	$SrcOut = IO::File->new("| gzip -c > $srcxml.gz");
	$TrgOut = IO::File->new("| gzip -c > $trgxml.gz");
    }
    else{
	$SrcOut = IO::File->new(">$srcxml");
	$TrgOut = IO::File->new(">$trgxml");
    }
    binmode($SrcOut,":encoding(utf-8)");
    binmode($TrgOut,":encoding(utf-8)");

    ## open XML writers for corpus documents
    my $SrcWriter = XML::Writer->new( OUTPUT => $SrcOut,
				      DATA_MODE => 1,
				      DATA_INDENT => 1 );
    $SrcWriter->xmlDecl("UTF-8");
    $SrcWriter->startTag("text");

    my $TrgWriter = XML::Writer->new( OUTPUT => $TrgOut,
				      DATA_MODE => 1,
				      DATA_INDENT => 1 );
    $TrgWriter->xmlDecl("UTF-8");
    $TrgWriter->startTag("text");

    print $AlgOut "<linkGrp targType=\"s\" fromDoc=\"$srcxml.gz\" toDoc=\"$trgxml.gz\" >\n";

    return ($SrcWriter,$TrgWriter);
}

sub close_output{
    my ($SrcWriter,$TrgWriter) = @_;
    $SrcWriter->endTag('text');
    $TrgWriter->endTag('text');
    $SrcWriter->end();
    $TrgWriter->end();
    print $AlgOut "  </linkGrp>\n";
}


## write sentences and return sentence IDs
sub write_sentences{
    my ($writer,$id,$sents,$origin) = @_;

    my @ids = ();
    if ( $#{$sents} ){
	my %attr = ( 'id' => "p$id" );
	# $attr{origin} = $origin if ($origin);
	$writer->startTag('p',%attr);
	my $sid = 0;
	foreach my $s (@{$sents}){
	    $sid++;
	    my %attr = ( 'id' => $id.'.'.$sid );
	    $attr{origin} = $origin if ($origin);
	    $writer->startTag('s',%attr);
	    $writer->characters($s);
	    $writer->endTag('s');
	    push(@ids,$id.'.'.$sid);
	}
	$writer->endTag('p');
    }
    else{
	my %attr = ( 'id' => $id );
	$attr{origin} = $origin if ($origin);
	$writer->startTag('s',%attr);
	$writer->characters($$sents[0]);
	$writer->endTag('s');
	push(@ids,$id);
    }
    return @ids;
}


